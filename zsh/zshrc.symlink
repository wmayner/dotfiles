# .zshrc of wmayner

# -----------------------------------------------------------------------------
# MUST BE FIRST
# -----------------------------------------------------------------------------
# Establish a minimal, known-good baseline PATH so core macOS tools are always
# available even if later initialization fails. Final PATH ordering is enforced
# near the end of the file.
export PATH="/usr/bin:/bin:/usr/sbin:/sbin:/opt/homebrew/bin:/opt/homebrew/sbin"
hash -r

# -----------------------------------------------------------------------------
# Apple Silicon safety: force arm64 shell if launched under Rosetta
# -----------------------------------------------------------------------------
# Prevent accidentally running an x86_64 shell that would pick up Intel
# Homebrew artifacts. The guard variable prevents infinite recursion.
if [[ "$(uname -m)" == "x86_64" ]] && [[ "$(sysctl -n hw.optional.arm64 2>/dev/null)" == "1" ]] && [[ -z "${ZSH_FORCE_ARM64_DONE:-}" ]]; then
  export ZSH_FORCE_ARM64_DONE=1
  exec arch -arm64 /bin/zsh -l
fi

# -----------------------------------------------------------------------------
# XDG base directories
# -----------------------------------------------------------------------------
export XDG_CACHE_HOME="$HOME/.cache"

# -----------------------------------------------------------------------------
# Early, machine-specific overrides
# -----------------------------------------------------------------------------
# Put system-specific early logic in ~/.zshrc.before.local so this file
# remains portable.
[ -f "$HOME/.zshrc.before.local" ] && source "$HOME/.zshrc.before.local"
# -----------------------------------------------------------------------------

# -----------------------------------------------------------------------------
# Environment variables
# -----------------------------------------------------------------------------
export DEFAULT_USER="will"
# Default editor
export EDITOR="vim"
export VISUAL="vim"
export DOTFILES="$HOME/dotfiles"
# -----------------------------------------------------------------------------

# -----------------------------------------------------------------------------
# Python / Conda
# -----------------------------------------------------------------------------
# Conda is kept available for legacy workflows, but we explicitly:
# - disable auto-activation of base
# - prevent installs into base
# - do NOT allow conda to dominate PATH by default
# Final PATH precedence is enforced later.

# Never auto-activate base
export CONDA_AUTO_ACTIVATE_BASE=false
# conda {
# >>> conda initialize >>>
# !! Contents within this block are managed by 'conda init' !!
__conda_setup="$('/Users/will/miniconda3/bin/conda' 'shell.zsh' 'hook' 2> /dev/null)"
if [ $? -eq 0 ]; then
    eval "$__conda_setup"
else
    if [ -f "/Users/will/miniconda3/etc/profile.d/conda.sh" ]; then
        . "/Users/will/miniconda3/etc/profile.d/conda.sh"
    else
        export PATH="/Users/will/miniconda3/bin:$PATH"
    fi
fi
unset __conda_setup
# <<< conda initialize <<<

# Prevent installing into base
function extended_conda() {
  if [ "${CONDA_PROMPT_MODIFIER-}" = "(base) " ] && [ "$1" = "install" ]; then
    echo "Not allowed in base environment"
  else
    conda "$@"
  fi
}
alias conda=extended_conda

# -----------------------------------------------------------------------------
# oh-my-zsh
# -----------------------------------------------------------------------------
# Path to your oh-my-zsh configuration
ZSH="$HOME/.oh-my-zsh"

# Path to your oh-my-zsh custom directory (default is .oh-my-zsh/custom/)
ZSH_CUSTOM=".oh-my-zsh/custom"

# Set name of the theme to load.
# Look in ~/.oh-my-zsh/themes/
# Optionally, if you set this to "random", it'll load a random theme each
# time that oh-my-zsh is loaded.
# ZSH_THEME="powerlevel10k/powerlevel10k"

# Example aliases
# alias zshconfig="mate ~/.zshrc"
# alias ohmyzsh="mate ~/.oh-my-zsh"

# Set to this to use case-sensitive completion
# CASE_SENSITIVE="true"

# Comment this out to disable bi-weekly auto-update checks
# DISABLE_AUTO_UPDATE="true"

# Uncomment to change how many often would you like to wait before
# auto-updates occur? (in days)
export UPDATE_ZSH_DAYS=14

# Uncomment following line if you want to disable colors in ls
# DISABLE_LS_COLORS="true"

# Uncomment following line if you want to disable autosetting terminal
# title.
# DISABLE_AUTO_TITLE="true"

# Uncomment following line if you want red dots to be displayed while
# waiting for completion
COMPLETION_WAITING_DOTS="true"

# Which plugins would you like to load? (plugins can be found in
# ~/.oh-my-zsh/plugins/*)
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
plugins=(
  brew git history-substring-search direnv
)
source "$ZSH/oh-my-zsh.sh"

# -----------------------------------------------------------------------------
# Tool configuration
# -----------------------------------------------------------------------------

# Bright highlight on matching history search
typeset -g HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_FOUND='fg=#ffffff,bg=#aa00aa,bold'
# Leave highlight on
typeset -g HISTORY_SUBSTRING_SEARCH_HIGHLIGHT_TIMEOUT=99999

# gpg
export GPG_TTY=$TTY

# zsh
setopt extendedglob

# Homebrew
export HOMEBREW_NO_ANALYTICS=1

# dircolors
autoload colors

# Prefer GNU ls if available; fall back to BSD ls
ls --color -d . &>/dev/null 2>&1 && alias ls='ls --color=auto -F' || alias ls='ls -GF'
dircolors &>/dev/null 2>&1 && eval "$(dircolors $DOTFILES/zsh/dircolors.ansi-dark)" || export LSCOLORS="Gxfxcxdxbxegedabagacad"
gdircolors &>/dev/null 2>&1 && eval "$(gdircolors $DOTFILES/zsh/dircolors.ansi-dark)" && alias ls='gls --color=auto -hF'
export GREP_COLORS='mt=1;31'

# less
# Use pygmentize for syntax highlighting if available
export LESSOPEN='|$HOME/.lessfilter %s'

# zoxide (fasd replacement)
eval "$(zoxide init zsh)"
alias j='z'
alias ji='zi'

# iTerm integration
[ -f "$HOME/.iterm2_shell_integration.zsh" ] && source "$HOME/.iterm2_shell_integration.zsh"

# asdf (loaded before final PATH so shims exist but do not dominate)
if [ -x "/opt/homebrew/bin/brew" ]; then
  . "$(/opt/homebrew/bin/brew --prefix asdf)/libexec/asdf.sh"
fi

# fzf
_gen_fzf_default_opts() {
  local base03="234"
  local base02="235"
  local base01="240"
  local base00="241"
  local base0="244"
  local base1="245"
  local base2="254"
  local base3="230"
  local yellow="136"
  local orange="166"
  local red="160"
  local magenta="125"
  local violet="61"
  local blue="33"
  local cyan="37"
  local green="64"

  # Comment and uncomment below for the light theme.

  # Solarized Dark color scheme for fzf
  export FZF_DEFAULT_OPTS="
    --reverse
    --color fg:-1,bg:-1,hl:$blue,fg+:$base2,bg+:$base02,hl+:$blue
    --color info:$yellow,prompt:$yellow,pointer:$base3,marker:$base3,spinner:$yellow
  "
  ## Solarized Light color scheme for fzf
  #export FZF_DEFAULT_OPTS="
  #  --color fg:-1,bg:-1,hl:$blue,fg+:$base02,bg+:$base2,hl+:$blue
  #  --color info:$yellow,prompt:$yellow,pointer:$base03,marker:$base03,spinner:$yellow
  #"
}
_gen_fzf_default_opts

# Accept history selection instead of putting it on the command line
fzf-history-widget-accept() {
  fzf-history-widget
  zle accept-line
}
zle     -N     fzf-history-widget-accept
bindkey '^X^R' fzf-history-widget-accept

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# pyphi
export PYPHI_WELCOME_OFF='yes'

# -----------------------------------------------------------------------------
# Keybindings / aliases
# -----------------------------------------------------------------------------

# Use vi to edit command line
bindkey -v

# Remove delay in entering command mode, as described by
# http://zsh.sourceforge.net/Guide/zshguide04.html:
#
# You can remove all bindings starting with a given prefix by adding the `-p
# option. The example given in the manual,
#     bindkey -rpM viins '\e'
# (except it uses the equivalent form `^[') is amongst the most useful, as it
# will remove the annoying delay after you type `\e' to enter vi command mode.
# The delay is there because the cursor keys usually also start with \e and
# the shell is waiting to see if you actually typed one of those.
# bindkey -rpM viins "\e"

# Use control+j and control+k for scrolling through history, since removing
# all bindings that begin with escape also removes the arrow-key bindings
bindkey "^K" history-substring-search-up
bindkey "^J" history-substring-search-down

# Press Ctrl+Q to push the current command on to the stack. It will disappear,
# allowing you to enter another command, after which it will reappear in the
# prompt.
bindkey "^Q" push-input

# List only directories
alias lsd="ls -d */"
# Allow colors with less
alias less="less -R"
# Get the absolute path of a file
alias fullpath="readlink -f"
# Use colordiff instead of native diff
alias diff="colordiff"
# Always sudo htop (doesn't work properly on macOS otherwise)
if [ "$OS" = "Darwin" ]; then
  alias htop="sudo htop"
fi
# list dir contents (with human-readable sizes)
alias l="ls -AhG1F"
alias ll="ls -lAhGF"
alias lsa="ls -AhGF"
# Safe rm
alias t="trash"
# Clear screen
alias c="clear"
# Clear screen and list contents
alias cl="clear && l"
# Clear screen and print directory tree
alias ck="clear && tree"
# Move up one directory
alias ..="cd .."
# Human-readable disk usage information
alias df="df -h"
# Forward port
alias forward="ssh -NL"
# Syntax highlighting with Pygments
alias hl="pygmentize -g"
# MATLAB binary
alias matlab="/Applications/MATLAB_R2016a.app/bin/matlab"
# ag with custom colors and restricted line width
alias ag="ag --color-line-number 34 --color-match 36 --color-path 32 --width 100"

# Editing shortcuts
alias v="vim"
# Edit configuration (from within dotfiles, for interacting with repo)
alias vrc='$EDITOR $(realpath $HOME/.vimrc)'

# git aliases
alias gp="git push"
# git diff
alias gc="git commit -m"
# git diff
alias gd="git d"
# git diff
alias gdc="git dc"
# git log
alias gl="git l"
# git log all branches
alias gla="git la"
# git branch
alias gb="git branch"
# git branch delete
alias gbd="git branch -d"
# git flow
alias gf="git flow"
# git flow feature
alias gff="git flow feature"
# git flow release
alias gfr="git flow release"
# git flow hotfix
alias gfh="git flow hotfix"
# fast git status
alias g="git status"
# edit .gitconfig
alias vgrc="$EDITOR $HOME/.gitconfig"

# python
alias p="python3"
alias python="python3"

# -----------------------------------------------------------------------------
# FINAL PATH ASSEMBLY
# -----------------------------------------------------------------------------
# At this point, conda and asdf may have mutated PATH. We now construct the
# final PATH *once* so precedence is explicit and stable.
typeset -U path
path=(
  $HOME/bin
  $HOME/dotfiles/bin
  $HOME/.local/bin
  /opt/homebrew/bin
  /opt/homebrew/sbin
  $HOME/.asdf/shims
  /opt/homebrew/opt/asdf/libexec/bin
  $HOME/miniconda3/condabin
  "/Applications/Docker.app/Contents/Resources/bin"
  "/Applications/Visual Studio Code.app/Contents/Resources/app/bin"
  /opt/homebrew/opt/postgresql@17/bin
  /opt/homebrew/opt/coreutils/libexec/gnubin
  /usr/bin /bin /usr/sbin /sbin /usr/texbin
)
export PATH="${(j/:/)path}"
hash -r

# -----------------------------------------------------------------------------
# zshrc.local
# -----------------------------------------------------------------------------
# Put local, non-portable overrides here.
[ -f "$HOME/.zshrc.local" ] && source "$HOME/.zshrc.local"

# Activate starship prompt
eval "$(starship init zsh)"
